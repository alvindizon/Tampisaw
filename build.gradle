import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektPlugin

// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'dependencies.gradle'

    ext {
        detektVersion = versions.detektVersion
    }
    addRepos(repositories)
    dependencies {
        classpath deps.AGP
        classpath deps.kotlinGradlePlugin
        classpath deps.androidX.navigation.safeArgsPlugin
        classpath deps.dagger.hiltPlugin
        classpath deps.googleServicesPlugin
        classpath deps.firebase.crashlyticsPlugin
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "$detektVersion"
}

allprojects {
    addRepos(repositories)
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            allWarningsAsErrors = true
            jvmTarget = "1.8" // this needs to be set if you want your decompiled Java code at Java 8
            freeCompilerArgs += ["-Xjvm-default=all"]
        }
    }
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    detekt {
        allRules = true // fail build on any finding
        parallel = true //Enables parallel compilation of source files
        config = files("$rootDir/detekt.yml")
    }
    plugins.withType(DetektPlugin) {
        tasks.withType(Detekt) {
            reports.sarif.required.set(false)
            reports.html.required.set(true)
            reports.xml.required.set(false)
            reports.txt.required.set(false)
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


